// *** Installs component "Apply Shamanic Dance improvements to Shaman kits" ***

// getting class id for Shaman
COPY_EXISTING ~clastext.2da~ ~override~
  LPF FIND_2DA_VALUE
  INT_VAR
    column = 1
    search_column = 0
  STR_VAR
    search_text = ~SHAMAN~
  RET
    row
    value
  END
BUT_ONLY

ACTION_IF (IS_AN_INT ~value~ && value > 0) BEGIN
  OUTER_SET classID = value

  // getting CLAB files from available Shaman kits
  OUTER_SET kitlist = 0
  COPY_EXISTING ~kitlist.2da~ ~override~
    SET row = 0
    WHILE (row >= 0) BEGIN
      LPF FIND_2DA_VALUE
      INT_VAR
        start_row = row
        column = 5
        search_column = 8
      STR_VAR
        search_text = EVAL ~%classID%~
      RET
        row
        value
      END

      PATCH_IF (row >= 0) BEGIN
        PATCH_IF (FILE_EXISTS_IN_GAME ~%value%.2DA~) BEGIN
          TEXT_SPRINT $kitlist(~%kitlist%~) ~%value%~
          SET kitlist += 1
        END
        SET row += 1
      END
    END
  BUT_ONLY

  // updating Shaman Dance behavior
  OUTER_FOR (idx = 0; idx < kitlist; ++idx) BEGIN
    OUTER_TEXT_SPRINT table_resref $kitlist(~%idx%~)
    COPY_EXISTING ~%table_resref%.2DA~ ~override~
      COUNT_2DA_COLS numCols
      COUNT_2DA_ROWS numCols numRows
      // scanning CLAB table for AP_ entries
      FOR (col = 1; col < numCols; ++col) BEGIN
        FOR (row = 0; row < numRows; ++row) BEGIN
          TEXT_SPRINT spl_resref ~~
          READ_2DA_ENTRY row col numCols value
          PATCH_IF (~%value%~ STRING_MATCHES_REGEXP ~[GA][AP]_.*~ = 0) BEGIN
            INNER_PATCH_SAVE resref ~%value%~ BEGIN REPLACE_TEXTUALLY ~^[GA][AP]_~ ~~ END
            PATCH_IF (FILE_EXISTS_IN_GAME ~%resref%.SPL~) BEGIN
              INNER_ACTION BEGIN
                // scanning spell for "Change Bard Song" resref
                COPY_EXISTING ~%resref%.SPL~ ~override~
                  READ_LONG 0x64 ofsAbils
                  READ_SHORT 0x68 numAbils
                  READ_LONG 0x6a ofsFx
                  READ_SHORT 0x6e firstFxGlobal
                  READ_SHORT 0x70 numFxGlobal

                  FOR (idxFx = 0; idxFx < numFxGlobal; ++idxFx) BEGIN
                    SET ofs = ofsFx + (firstFxGlobal + idxFx) * 0x30
                    READ_SHORT ofs opcode
                    PATCH_IF (opcode = 251) BEGIN   // Change Bard Song
                      READ_ASCII (ofs + 0x14) spl_resref (8) NULL
                      PATCH_IF (NOT VARIABLE_IS_SET $resref_cache(~%spl_resref%~) || $resref_cache(~%spl_resref%~) < 2) BEGIN
                        // update each spell instance only once
                        SET $resref_cache(~%spl_resref%~) = 1
                      END
                      SET idxFx = numFxGlobal
                    END
                  END

                  PATCH_IF (~%spl_resref%~ STR_EQ ~~) BEGIN
                    FOR (idxAbil = 0; idxAbil < numAbils; ++idxAbil) BEGIN
                      SET ofs = ofsAbils + idxAbil * 0x28
                      READ_SHORT (ofs + 0x1e) numFx
                      READ_SHORT (ofs + 0x20) firstFx
                      FOR (idxFx = 0; idxFx < numFx; ++idxFx) BEGIN
                        SET ofs2 = ofsFx + (firstFx + idxFx) * 0x30
                        READ_SHORT ofs2 opcode
                        PATCH_IF (opcode = 251) BEGIN   // Change Bard Song
                          READ_ASCII (ofs2 + 0x14) spl_resref (8) NULL
                          PATCH_IF (NOT VARIABLE_IS_SET $resref_cache(~%spl_resref%~) || $resref_cache(~%spl_resref%~) < 2) BEGIN
                            // update each spell instance only once
                            SET $resref_cache(~%spl_resref%~) = 1
                          END
                          SET idxFx = numFx
                          SET idxAbil = numAbils
                        END
                      END
                    END
                  END
                BUT_ONLY
              END
            END

            PATCH_IF (VARIABLE_IS_SET $resref_cache(~%spl_resref%~) && $resref_cache(~%spl_resref%~) = 1 &&
                      FILE_EXISTS_IN_GAME ~%spl_resref%.SPL~) BEGIN
              SET $resref_cache(~%spl_resref%~) = 2
              INNER_ACTION BEGIN
                // updating Shaman Dance behavior
                COPY_EXISTING ~%spl_resref%.SPL~ ~override~
                  LPF DELETE_SPELL_EFFECT
                    INT_VAR opcode_to_delete = 363  // Movement rate check
                  END
                BUT_ONLY
              END
            END
          END
        END
      END
    BUT_ONLY
  END
END
