// *** Add shaman-specific items to BG2:EE ***

INCLUDE ~%MOD_FOLDER%/lib/items_prep.tph~

// The Soulherder's Staff +2
LAF INSTALL_SOULHERDERS_STAFF END

// adding item to Troll Mound in the Druid Grove
COPY_EXISTING ~AR1904.ARE~ ~override~
  LPF ADD_AREA_ITEM
  INT_VAR
    container_to_add_to = 2
  STR_VAR
    item_to_add = ~A7#STAF1~
  END
BUT_ONLY

// Staff is not available if original staff from SoD is detected
<<<<<<<< .../inlined/items/a7#staf1.baf
IF
  Global("A7#STAF1","AR1904",0)
  PartyHasItem("BDSTAF02")
THEN
  RESPONSE #100
    SetGlobal("A7#STAF1","AR1904",1)
    ActionOverride("Container2",DestroyItem("A7#STAF1"))
END
>>>>>>>>
EXTEND_BOTTOM ~AR1904.BCS~ ~.../inlined/items/a7#staf1.baf~


// Shroud of Souls
LAF INSTALL_SHROUD_OF_SOULS
INT_VAR
  ac_bonus = 2
END

// add item to Kuo-toa priest in Abazigal's Lair
COPY_EXISTING ~GORKUO03.CRE~ ~override/a7#kuo03.cre~
  ADD_CRE_ITEM ~A7#CLCK1~ #0 #0 #0 ~none~ ~inv1 inv2 inv3 inv4 inv5 inv6 inv7 inv8 inv9 inv10 inv11 inv12 inv13 inv14 inv15 inv16~

COPY_EXISTING ~AR6002.ARE~ ~override~
  LPF fj_are_structure
  INT_VAR
    fj_loc_x = 1110
    fj_loc_y = 476
    fj_dest_x = 1110
    fj_dest_y = 476
    fj_orientation = 0  // South
    fj_animation = 0x7f0c // KUO-TOA
  STR_VAR
    fj_structure_type = ~actor~
    fj_name = ~A7# Kuo-Toa Priest~
    fj_cre_resref = ~A7#KUO03~
  END


// Spirit Guide +4
LAF INSTALL_SPIRIT_GUIDE END

// adding item to container in Watcher's Keep, Level 4, Illithid Lair
COPY_EXISTING ~AR3021.ARE~ ~override~
  LPF ADD_AREA_ITEM
  INT_VAR
    container_to_add_to = 2
  STR_VAR
    item_to_add = ~A7#LEAT1~
  END
BUT_ONLY


// Updating script
<<<<<<<< .../inlined/items/a7#leat1_insert.baf
IF
  Global("A7#LEAT1","LOCALS",0)
  HasItem("BDSPIRIM",Myself)
  HasItemEquiped("A7#LEAT1",LastSummonerOf(Myself))
THEN
  RESPONSE #50
    SetGlobal("A7#LEAT1","LOCALS",1)
    ApplySpellRES("A7#LEATX",Myself)
    Continue()
  RESPONSE #50
    SetGlobal("A7#LEAT1","LOCALS",1)
    TransformItem("BDSPIRIM","A7#SPRT1")
    ApplySpellRES("A7#LEAT1",Myself)
END

IF
  Global("A7#LEAT1","LOCALS",0)
  HasItem("BDSPNAIM",Myself)
  HasItemEquiped("A7#LEAT1",LastSummonerOf(Myself))
THEN
  RESPONSE #50
    SetGlobal("A7#LEAT1","LOCALS",1)
    ApplySpellRES("A7#LEATX",Myself)
    Continue()
  RESPONSE #50
    SetGlobal("A7#LEAT1","LOCALS",1)
    TransformItem("BDSPNAIM","A7#SPRT2")
    ApplySpellRES("A7#LEAT1",Myself)
END

>>>>>>>>

COPY_EXISTING ~bdshunsu.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    PATCH_FOR_EACH item IN ~A7#STAF1~ ~BDHELM16~ ~BDAMUL26~ BEGIN
      PATCH_IF (INDEX_BUFFER(~"%item%"~) < 0) BEGIN
        SET ofs = INDEX_BUFFER(~Allegiance(Myself,EVILCUTOFF)~)
        PATCH_IF (ofs > 0) BEGIN
          SET ofs = RINDEX_BUFFER(~^IF$~ ofs)
          PATCH_IF (ofs >= 0) BEGIN
            PATCH_IF (~%item%~ STR_EQ ~A7#STAF1~) BEGIN TEXT_SPRINT cmd_equipped ~HasItemEquipedReal~ END ELSE BEGIN TEXT_SPRINT cmd_equipped ~HasItemEquiped~ END
            TEXT_SPRINT block ~IF Global("%item%","LOCALS",0) %cmd_equipped%("%item%",LastSummonerOf(Myself)) THEN RESPONSE #100 SetGlobal("%item%","LOCALS",1) ApplySpellRES("%item%",Myself) END%LNL%~
            SET strlen = STRING_LENGTH ~%block%~
            INSERT_BYTES ofs strlen
            WRITE_ASCIIE ofs ~%block%~ (strlen)
          END
        END
      END
    END

    SET ofs = INDEX_BUFFER(~Allegiance(Myself,EVILCUTOFF)~)
    PATCH_IF (ofs > 0) BEGIN
      SET ofs = RINDEX_BUFFER(~^IF$~ ofs)
      PATCH_IF (ofs >= 0) BEGIN
        INSERT_FILE ofs ~.../inlined/items/a7#leat1_insert.baf~
      END
    END
  END
BUT_ONLY
