// *** Unlocks another tier of spirits by the shamanic dance ***

// Increasing max. summon limit
COPY_EXISTING ~summlimt.2da~ ~override~
  COUNT_2DA_COLS numCols
  COUNT_2DA_ROWS numCols numRows
  FOR (row = 0; row < numRows; row += 1) BEGIN
    READ_2DA_ENTRY row 0 numCols type
    PATCH_IF (~%type%~ STRING_EQUAL_CASE ~NORMAL~) BEGIN
      READ_2DA_ENTRY row 1 numCols count
      PATCH_IF (IS_AN_INT "count" && count < 6) BEGIN
        SET_2DA_ENTRY row 1 numCols 6
        SET row = numRows
      END
    END
  END


// Expanding shamanic dance
COPY ~%MOD_FOLDER%/tables/a7sham24.2da~ ~override~

/*
Strref codes:
1: You cannot summon any more creatures. (BG1: 24529, BG2: 74090, IWD: 34748)
2: No spirits answer your call. (BG1: 69613, BG2: 103120, IWD: 40909)
3: A minor animal spirit answers your call. (BG1: 69614, BG2: 103121, IWD: 40910)
4: A major animal spirit answers your call. (BG1: 69615, BG2: 103122, IWD: 40911)
5: A minor nature spirit answers your call. (BG1: 69616, BG2: 103123, IWD: 40912)
6: A major nature spirit answers your call. (BG1: 69617, BG2: 103124, IWD: 40913)
7: A grand nature spirit answers your call.
8: Protected by spirits.
 */
ACTION_IF (~%LANGUAGE%~ STRING_EQUAL_CASE ~french~) BEGIN
  OUTER_SET v1 = RESOLVE_STR_REF(@1017)
  OUTER_SET v2 = RESOLVE_STR_REF(@1012)
  OUTER_SET v3 = RESOLVE_STR_REF(@1013)
  OUTER_SET v4 = RESOLVE_STR_REF(@1014)
  OUTER_SET v5 = RESOLVE_STR_REF(@1015)
  OUTER_SET v6 = RESOLVE_STR_REF(@1016)
END ELSE BEGIN
  ACTION_IF (GAME_IS ~bgee~) BEGIN
    OUTER_SET v1 = 24529
    OUTER_SET v2 = (GAME_INCLUDES ~sod~) ? 69613 : 32402
    OUTER_SET v3 = (GAME_INCLUDES ~sod~) ? 69614 : 32401
    OUTER_SET v4 = (GAME_INCLUDES ~sod~) ? 69615 : 32403
    OUTER_SET v5 = (GAME_INCLUDES ~sod~) ? 69616 : 32404
    OUTER_SET v6 = (GAME_INCLUDES ~sod~) ? 69617 : 32405
  END ELSE ACTION_IF (GAME_IS ~iwdee~) BEGIN
    OUTER_SET v1 = 34748
    OUTER_SET v2 = 40909
    OUTER_SET v3 = 40910
    OUTER_SET v4 = 40911
    OUTER_SET v5 = 40912
    OUTER_SET v6 = 40913
  END ELSE BEGIN  // BG2EE, EET
    OUTER_SET v1 = 74090
    OUTER_SET v2 = 103120
    OUTER_SET v3 = 103121
    OUTER_SET v4 = 103122
    OUTER_SET v5 = 103123
    OUTER_SET v6 = 103124
  END
END
OUTER_SET v7 = RESOLVE_STR_REF(@200)  // A grand nature spirit answers your call.
OUTER_SET v8 = RESOLVE_STR_REF(@204)  // Shaman is protected by spirits
ACTION_DEFINE_ASSOCIATIVE_ARRAY strrefs BEGIN
  1 => ~%v1%~
  2 => ~%v2%~
  3 => ~%v3%~
  4 => ~%v4%~
  5 => ~%v5%~
  6 => ~%v6%~
  7 => ~%v7%~
  8 => ~%v8%~
END

DEFINE_PATCH_MACRO A7GET_STRREF_CODE
BEGIN
  SET strref = "-1"
  PATCH_IF (code >= 1 && code <= 8) BEGIN
    SET strref = $EVAL strrefs(~%code%~)
  END
END

COPY ~%MOD_FOLDER%/spells/spsh004.spl~ ~override~
  READ_LONG 0x64 ofsAbil
  READ_SHORT 0x68 numAbil
  READ_LONG 0x6a ofsEffects

  FOR (idxAbil = 0; idxAbil < numAbil; idxAbil += 1) BEGIN
    SET ofsCurAbil = ofsAbil + (idxAbil * 0x28)
    READ_SHORT (ofsCurAbil + 0x1e) numAbilEffects
    READ_SHORT (ofsCurAbil + 0x20) idxFirstAbilEffect
    FOR (idxEffect = idxFirstAbilEffect; idxEffect < (idxFirstAbilEffect + numAbilEffects); idxEffect += 1) BEGIN
      SET ofsCurEffect = ofsEffects + (idxEffect * 0x30)
      READ_SHORT (ofsCurEffect + 0x00) opcode
      PATCH_IF (opcode = 318) BEGIN
        READ_LONG (ofsCurEffect + 0x2c) code
        LPM A7GET_STRREF_CODE
        WRITE_LONG (ofsCurEffect + 0x2c) strref
      END ELSE PATCH_IF (opcode = 139) BEGIN
        READ_LONG (ofsCurEffect + 0x04) code
        LPM A7GET_STRREF_CODE
        WRITE_LONG (ofsCurEffect + 0x04) strref
      END
    END
  END


// Greater Fire Spirit
COPY ~%MOD_FOLDER%/creatures/a7sha24a.cre~ ~override~
  SAY NAME1 @201  // Greater Fire Spirit
  SAY NAME2 @201  // Greater Fire Spirit

COPY ~%MOD_FOLDER%/items/a7sha24a.itm~ ~override~


// Greater Air Spirit
COPY ~%MOD_FOLDER%/creatures/a7sha24b.cre~ ~override~
  SAY NAME1 @202  // Greater Air Spirit
  SAY NAME2 @202  // Greater Air Spirit

COPY ~%MOD_FOLDER%/items/a7sha24b.itm~ ~override~
     ~%MOD_FOLDER%/spells/a7sh24b.spl~ ~override~
     ~%MOD_FOLDER%/spells/a7sh24e.eff~ ~override~


// Greater Earth Spirit
COPY ~%MOD_FOLDER%/creatures/a7sha24c.cre~ ~override~
  SAY NAME1 @203  // Greater Earth Spirit
  SAY NAME2 @203  // Greater Earth Spirit

COPY ~%MOD_FOLDER%/items/a7sha24c.itm~ ~override~


// Updating class description
LAF UPDATE_CLASS_DESC
  INT_VAR newDesc = RESOLVE_STR_REF(@101)
  STR_VAR className = "SHAMAN"
END
