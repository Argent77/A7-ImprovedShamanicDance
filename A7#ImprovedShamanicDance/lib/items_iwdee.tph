// *** Add shaman-specific items to IWD:EE ***

INCLUDE ~%MOD_FOLDER%/lib/items_prep.tph~

// The Soulherder's Staff +2
LAF INSTALL_SOULHERDERS_STAFF RET installed END
ACTION_IF (installed) BEGIN
  // adding item to Orc Shaman in Severed Hand, second level
  <<<<<<<< .../inlined/a7#staf.baf
  IF
    AreaCheck("AR5002")
    Global("BDSTAF02","LOCALS",0)
  THEN
    RESPONSE #100
      SetGlobal("BDSTAF02","LOCALS",1)
      CreateItem("bdstaf02",0,0,0)
      Continue()
  END
  >>>>>>>>
  COPY_EXISTING ~ar5002.are~ ~override~
    READ_LONG 0x54 ofsActors
    READ_SHORT 0x58 numActors
    FOR (idx = 0; idx < numActors; ++idx) BEGIN
      SET ofs = ofsActors + idx * 0x110
      READ_ASCII (ofs + 0x80) resref (8) NULL
      PATCH_IF (~%resref%~ STR_EQ ~SDORCSHM~) BEGIN
        READ_ASCII (ofs + 0x50) script (8) NULL
        PATCH_IF (~%script%~ STR_EQ ~~ || NOT FILE_EXISTS_IN_GAME ~%script%.BCS~) BEGIN
          INNER_ACTION BEGIN COMPILE ~.../inlined/a7#staf.baf~ END
          WRITE_ASCII (ofs + 0x50) ~A7#STAF~ (8)
        END ELSE BEGIN
          INNER_ACTION BEGIN EXTEND_TOP ~%script%.BCS~ ~.../inlined/a7#staf.baf~ END
        END
        SET idx = numActors
      END
    END
  BUT_ONLY
END


// Circlet of Lost Souls
LAF INSTALL_CIRCLET_OF_LOST_SOULS RET installed END
ACTION_IF (installed) BEGIN
  // adding item to Skeletal Mage in the Vale of Shadows Crypt
<<<<<<<< .../inlined/a7#helm.baf
IF
  AreaCheck("AR3502")
  Global("BDHELM16","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("BDHELM16","LOCALS",1)
    CreateItem("bdhelm16",0,0,0)
    Continue()
END
>>>>>>>>
  COPY_EXISTING ~ar3502.are~ ~override~
    READ_LONG 0x54 ofsActors
    READ_SHORT 0x58 numActors
    FOR (idx = 0; idx < numActors; ++idx) BEGIN
      SET ofs = ofsActors + idx * 0x110
      READ_ASCII (ofs + 0x80) resref (8) NULL
      PATCH_IF (~%resref%~ STR_EQ ~KTSMG2~) BEGIN
        READ_ASCII (ofs + 0x50) script (8) NULL
        PATCH_IF (~%script%~ STR_EQ ~~ || NOT FILE_EXISTS_IN_GAME ~%script%.BCS~) BEGIN
          INNER_ACTION BEGIN COMPILE ~.../inlined/a7#helm.baf~ END
          WRITE_ASCII (ofs + 0x50) ~A7#HELM~ (8)
        END ELSE BEGIN
          INNER_ACTION BEGIN EXTEND_TOP ~%script%.BCS~ ~.../inlined/a7#helm.baf~ END
        END
        SET idx = numActors
      END
    END
  BUT_ONLY
END


// Heart of the Mountain
LAF INSTALL_HEART_OF_THE_MOUNTAIN RET installed END
ACTION_IF (installed) BEGIN
  // adding item to Krilag in Dorn's Deep
  COPY_EXISTING ~neoorogc.cre~ ~override~
    ADD_CRE_ITEM ~BDAMUL26~ #0 #0 #0 ~none~ ~inv1 inv2 inv3 inv4 inv5 inv6 inv7 inv8 inv9 inv10 inv11 inv12 inv13 inv14 inv15 inv16~
  BUT_ONLY
END


// Shroud of Souls
LAF INSTALL_SHROUD_OF_SOULS END

// add item to HoW Seer
COPY_EXISTING ~seer.cre~ ~override~
              ~seercut.cre~ ~override~
              ~seercut2.cre~ ~override~
  ADD_CRE_ITEM ~A7#CLCK1~ #0 #0 #0 ~none~ ~inv1 inv2 inv3 inv4 inv5 inv6 inv7 inv8 inv9 inv10 inv11 inv12 inv13 inv14 inv15 inv16~
BUT_ONLY IF_EXISTS


// Spirit Guide +4
LAF INSTALL_SPIRIT_GUIDE END

// adding item to a rakshasa inventory in TotLM castle
COPY_EXISTING ~rakshinv.cre~ ~override/a7#rak01.cre~
  ADD_CRE_ITEM ~A7#LEAT1~ #0 #0 #0 ~none~ ~inv1 inv2 inv3 inv4 inv5 inv6 inv7 inv8 inv9 inv10 inv11 inv12 inv13 inv14 inv15 inv16~
COPY_EXISTING ~ar9711.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    SET counter = 0
    REPLACE_EVALUATE ~"\(rakshinv\)"~
      BEGIN
        PATCH_IF (counter = 0) BEGIN
          TEXT_SPRINT replace ~a7#rak01~
        END ELSE BEGIN
          TEXT_SPRINT replace ~%MATCH1%~
        END
        SET counter = 1
      END
      ~"%replace%"~
  END
BUT_ONLY


// Updating script
<<<<<<<< .../inlined/items/a7#leat1_insert.baf
IF
  Global("A7#LEAT1","LOCALS",0)
  HasItem("BDSPIRIM",Myself)
  HasItemEquiped("A7#LEAT1",LastSummonerOf(Myself))
THEN
  RESPONSE #50
    SetGlobal("A7#LEAT1","LOCALS",1)
    ApplySpellRES("A7#LEATX",Myself)
    Continue()
  RESPONSE #50
    SetGlobal("A7#LEAT1","LOCALS",1)
    TransformItem("BDSPIRIM","A7#SPRT1")
    ApplySpellRES("A7#LEAT1",Myself)
END

IF
  Global("A7#LEAT1","LOCALS",0)
  HasItem("BDSPNAIM",Myself)
  HasItemEquiped("A7#LEAT1",LastSummonerOf(Myself))
THEN
  RESPONSE #50
    SetGlobal("A7#LEAT1","LOCALS",1)
    ApplySpellRES("A7#LEATX",Myself)
    Continue()
  RESPONSE #50
    SetGlobal("A7#LEAT1","LOCALS",1)
    TransformItem("BDSPNAIM","A7#SPRT2")
    ApplySpellRES("A7#LEAT1",Myself)
END

>>>>>>>>

COPY_EXISTING ~bdshunsu.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    PATCH_FOR_EACH item IN ~BDSTAF02~ ~BDHELM16~ ~BDAMUL26~ BEGIN
      PATCH_IF (INDEX_BUFFER(~"%item%"~) < 0) BEGIN
        SET ofs = INDEX_BUFFER(~Allegiance(Myself,EVILCUTOFF)~)
        PATCH_IF (ofs > 0) BEGIN
          SET ofs = RINDEX_BUFFER(~^IF$~ ofs)
          PATCH_IF (ofs >= 0) BEGIN
            PATCH_IF (~%item%~ STR_EQ ~BDSTAF02~) BEGIN TEXT_SPRINT cmd_equipped ~HasItemEquipedReal~ END ELSE BEGIN TEXT_SPRINT cmd_equipped ~HasItemEquiped~ END
            TEXT_SPRINT block ~IF Global("%item%","LOCALS",0) %cmd_equipped%("%item%",LastSummonerOf(Myself)) THEN RESPONSE #100 SetGlobal("%item%","LOCALS",1) ApplySpellRES("%item%",Myself) END%LNL%~
            SET strlen = STRING_LENGTH ~%block%~
            INSERT_BYTES ofs strlen
            WRITE_ASCIIE ofs ~%block%~ (strlen)
          END
        END
      END
    END

    SET ofs = INDEX_BUFFER(~Allegiance(Myself,EVILCUTOFF)~)
    PATCH_IF (ofs > 0) BEGIN
      SET ofs = RINDEX_BUFFER(~^IF$~ ofs)
      PATCH_IF (ofs >= 0) BEGIN
        INSERT_FILE ofs ~.../inlined/items/a7#leat1_insert.baf~
      END
    END
  END
BUT_ONLY
